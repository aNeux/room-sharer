plugins {
    id 'war'
}

ext {
    war_name = 'room-sharer.war'
}

group 'ru.ksu'
version '1.0-alpha'
project.webAppDirName = 'webapp'
compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        java {
            srcDirs 'src/', 'websrc/'
        }
    }
}

dependencies {
    compile "com.sun.faces:jsf-api:2.1.7"
    compile "com.sun.faces:jsf-impl:2.1.7"

    compile 'org.primefaces:primefaces:6.2'
    compile 'net.bootsfaces:bootsfaces:1.4.2'
    implementation fileTree('libs').include('*.jar')
    compile "javax.servlet:javax.servlet-api:3.0.1"
    implementation 'javax.el:el-api:2.2'

    compile 'org.slf4j:slf4j-api:1.7.13'
    compile 'org.slf4j:slf4j-log4j12:1.7.13'
    compile 'org.apache.commons:commons-lang3:3.10'
    compile 'org.apache.commons:commons-collections4:4.4'

    compile 'com.fasterxml.jackson.core:jackson-core:2.9.8'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.8'

    compile 'io.netty:netty-all:4.1.42.Final'
}

war {
    archiveName = war_name
}

task copyWar(type: Copy, dependsOn: war) {
    from "$buildDir/libs/${war_name}"
    into 'jetty/webapps/'
}

task runRoomSharer(type: JavaExec,  group: 'Room-Sharer', dependsOn: copyWar) {
    workingDir = file('jetty')
    classpath = files('jetty/start.jar')
    main = 'org.eclipse.jetty.start.Main'
    jvmArgs '-Xdebug', '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
}

task releaseBuild(type: Zip, group: 'Room-Sharer', dependsOn: war) {
    from('.') {
        include 'cfg/'
        exclude 'cfg/users_rooms/'
    }

    from("$buildDir/libs/") {
        include "${war_name}"
    }

    archiveBaseName = 'roomsharer-server'
    archiveVersion = new Date().format('ddMMyyyy.HHmm')
    archiveExtension = 'zip'
    destinationDirectory = file("$buildDir/release")
}
